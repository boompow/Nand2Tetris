// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //// Replace this comment with your code.
    //zeroing x
    Mux16(a=x , b=false , sel=zx , out=zeroedx );

    // bitwise negate x
    Not16(in=zeroedx , out=NotZeroedx );
    Mux16(a=zeroedx , b=NotZeroedx , sel=nx , out=negatedx );

    //zeroing y
    Mux16(a=y , b=false , sel=zy , out=zeroedy );

    // bitwise negate x
    Not16(in=zeroedy , out=NotZeroedy );
    Mux16(a=zeroedy , b=NotZeroedy , sel=ny , out=negatedy );

    //now lets build the f
    //if f = 1, binary addition
    Add16(a=negatedx , b=negatedy , out=xplusy );

    //if f = 0, bitwise AND
    And16(a=negatedx , b=negatedy , out=xandy );

    Mux16(a=xandy , b=xplusy , sel=f , out=xfy );


    //output bitwise negation
    Not16(in=xfy , out=xnotfy );

    //who knew there was something called sub-busting, I applied that to get parts of the 
    //output so that I can use them in internal pin and not get an error
    Mux16(a=xfy , b=xnotfy , sel=no , out=out, out[0..7]=lower8, out[8..15]=upper8, out[15]=sign );


    //ng
    Not(in=sign , out=notout );
    Not(in=notout , out=ng );


    //zr
    Or8Way(in=lower8 , out=upperOut );
    Or8Way(in=upper8 , out=lowerOut );

    Or(a=upperOut , b=lowerOut , out=isitone );

    Mux(a=true , b=false , sel=isitone , out=zr );

    
    
}