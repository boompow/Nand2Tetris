// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/5/CPU.hdl
/**
 * The Hack Central Processing unit (CPU).
 * Parses the binary code in the instruction input and executes it according to the
 * Hack machine language specification. In the case of a C-instruction, computes the
 * function specified by the instruction. If the instruction specifies to read a memory
 * value, the inM input is expected to contain this value. If the instruction specifies
 * to write a value to the memory, sets the outM output to this value, sets the addressM
 * output to the target address, and asserts the writeM output (when writeM = 0, any
 * value may appear in outM).
 * If the reset input is 0, computes the address of the next instruction and sets the
 * pc output to that value. If the reset input is 1, sets pc to 0.
 * Note: The outM and writeM outputs are combinational: they are affected by the
 * instruction's execution during the current cycle. The addressM and pc outputs are
 * clocked: although they are affected by the instruction's execution, they commit to
 * their new values only in the next cycle.
 */
CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
	//lets first unload the instruction - A instruction (0vvvvvvvvvvvvvv) and C-instruction (111accccccdddjjj)
    //A-instruction is if instruction[15] is 0
    //C-instruction is if instruction[15], instruction[14], instruction[13] are 1, then
    //then a is instruction[12]
    //then the ALU compute is instruction[6..11] - sent to the ALU
    //then the destination is instruction[3..5]
    //then the jump is instruction[0..2] - sent to the PC

    //0. check the type of instruction
    And(a=instruction[15], b=instruction[14], out=c12);
    And(a=c12, b=instruction[13], out=isC);
    Not(in=isC, out=isA);


    //1. the A-register
    Mux16(a=instruction , b=ALUOut , sel=isC , out=AregisterIn );
    //if the Op code is 1  and the A-desitination is 1, then ALU output can be written on the A-register
    And(a=isC , b=instruction[5] , out=CinstAdest );
    Or(a=isA , b=CinstAdest , out=writeonARegister );
    ARegister(in=AregisterIn , load=writeonARegister , out=AregisterOut, out[0..14]=addressM );
    //if writeonRegisrer is 1, then the ARegister output is ALUout, else it is the A-instruction
    

    //2. the D-register
    //make sure the instruction is C-instruction
    And(a=isC , b=instruction[4] , out=destD );
    DRegister(in=ALUOut , load=destD , out=DregisterOut );

    //3. the ALU
    //make sure the instruction is C-instruction
    And(a=isC , b=instruction[12] , out=selectM );
    Mux16(a=AregisterOut , b=inM , sel=selectM , out=ALUMuxOut );
    ALU(x=DregisterOut , y=ALUMuxOut , zx=instruction[11] , nx=instruction[10] , 
    zy=instruction[9] , ny=instruction[8] , f=instruction[7] , no=instruction[6] ,
     out=ALUOut , zr=ALUOutZ , ng=ALUOutN );
    //if 3rd instruction bit is 1, then Mout is ALUOut

    //4. the PC
    //make sure the instruction is C-instruction
    And(a=isC , b=instruction[0] , out=j0 );
    And(a=isC , b=instruction[1] , out=j1 );
    And(a=isC , b=instruction[2] , out=j2 );

    //comp
    Not(in=ALUOutZ , out=NotALUOutZ );
    Not(in=ALUOutN , out=NotALUOutN );
    //greater
    And(a=NotALUOutZ , b=NotALUOutN , out=greater );
    //equal 
    //ALUOutZ
    //lesser
    //ALUoutN

    //jumps based on their name in chapter 4

    //JGT
    And(a=j0 , b=greater , out=JGT );

    //JEQ
    And(a=j1 , b=ALUOutZ , out=JEQ );

    // JGE = JGT OR JEQ

    //JLT
    And(a=j2 , b=ALUOutN , out=JLT );

    // JNE = JGT OR JLT

    // JLE = JEQ OR JLT

    // JMP - the unconditional jump = JGT OR JEQ OR JLT

    // jump = JGT OR JEQ OR JLT = JNE OR JLE OR JMP
    Or(a=JGT , b=JEQ , out=subJump );
    Or(a=subJump , b=JLT , out=jump );

    // increment if Not jump
    Not(in=jump , out=increment );
   
    PC(in=AregisterOut , load=jump , inc=increment , reset=reset , out=pc16, out[0..14]=pc );


    //the annoying thing is representing direct equals, 
    //the Mout is ALUOut, 
    //and the writeM is equal to instruction[4]
     And16(a=ALUOut , b=ALUOut , out=outM );
     And(a=isC , b=instruction[3] , out=writeM );
}