// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/5/Memory.hdl
/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load=1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid and reads 0. Access to any address
 * in the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the Screen
 * and Keyboard chip specifications given in the lectures and the book.
 */
CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
	//First lets list out the binary of the maximum address of RAM16K, the screen, and the keyboard
    //RAM16K -   0|01|1111111111111
    //Screen -   0|10|1111111111111
    //Keyboard - 0|11|0000000000000
    //since we use the 24576th address for the Keyboard, the rest of the bits trailing the 13th bit are
    //going to be 0, unlike the rest because the rest are ranges
    //but as you can see the 14th and 13th bit are what determine the addressed RAM16K, Screen, or keyboard
    //if the 14th bit is zero then it is RAM16K
    //else if the 14th bit is one and the 13th bit is 0, then it's a Screen
    //else if both the 14th and 13th bits are 1, then it's a keyboard

    //now lets implement
    Not(in=address[14] , out=NotAddress14 );
    And(a=load , b=NotAddress14 , out=loadRAM );
    Not(in=address[13] , out=NotAddress13 );
    And(a=address[14] , b=NotAddress13 , out=loadIntermediateII );
    And(a=load , b=loadIntermediateII , out=loadScreen );

    RAM16K(in=in , load=loadRAM , address=address[0..13] , out=outRAM );
    Screen(in=in , load=loadScreen , address=address[0..12] , out=outScreen );
    Keyboard(out=outKeyboard );

    Mux16(a=outRAM , b=outScreen , sel=address[14] , out=outIntermediate );
    And(a=address[14] , b=address[13] , out=KeyboardSelector );
    Mux16(a=outIntermediate , b=outKeyboard , sel=KeyboardSelector , out=out );

}
